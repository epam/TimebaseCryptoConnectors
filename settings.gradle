import java.nio.file.Files
import java.nio.file.Paths

def excludingProjects = ['buildSrc'] as Set

def discoverSubprojects
discoverSubprojects = { File baseFolder, File currentFolder, List toCollection ->
    currentFolder.eachDir() { dir ->

        def relative = baseFolder.toPath().relativize(dir.toPath())
        def projectName = relative.toString().replace(File.separatorChar, ':' as char)

        if (Files.exists(Paths.get(dir.getAbsolutePath(), 'build.gradle')) && Files.exists(Paths.get(dir.getAbsolutePath(), 'src'))) {
            println "Found project: ${projectName}"

            if (!excludingProjects.contains(projectName)) {
                toCollection.add(projectName)
            }
        } else {
            discoverSubprojects(baseFolder, dir, toCollection)
        }

    }
    return toCollection
}

include discoverSubprojects(rootDir, new File(rootDir, "java"), new ArrayList()) as String[]

